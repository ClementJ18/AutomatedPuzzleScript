\subsection{Parsing}
We address the need for a reusable and maintainable parser by creating a formal syntax definition in the form of a grammar. In our design, we make several important changes to this phase. However, we want our redesign to remain compatible with the PuzzleScript design. As such, it is important that this phase's user-facing endpoints do not change. The main design change in this phase is that we switch from using a hand-crafted parser to using Rascal's Syntax Definition feature\footnote{\url{https://docs.rascal-mpl.org/stable/RascalConcepts/\#RascalConcepts-SyntaxDefinitionAndParsing}}\dd. This decision allows us to create a declarative representation of a PuzzleScript file that consists of lexicals and symbols. This representation is human-readable and easier to extend than a line-based parser. 

A Rascal syntax definition consists of a layout, keywords, lexicals, and symbols. The layout is a pattern that matches the white space and comments, it can be seen at the top of Figure \ref{fig:PuzzleScript Lexicals}. Symbols are composed of lexicals, literals, and other symbols. We use lexicals to parse individual tokens and then group them into symbols that represent PuzzleScript components such as Objects, Rules, Sprites and many more. Figure \ref{fig:syntax_example} shows a simple example using practically all Syntax Definition features.

\begin{figure}[!t]
\begin{lstlisting}[language=rascal]    
// layout is lists of whitespace characters
layout MyLayout = [\t\n\ \r\f]*;

// identifiers are characters of lowercase alphabet letters, 
// not immediately preceded or followed by those (longest match)
// and not any of the reserved keywords
lexical Identifier = [a-z] !<< [a-z]+ !>> [a-z] \ MyKeywords;

// this defines the reserved keywords used in the definition of Identifier
keyword MyKeywords = "if" | "then" | "else" | "fi";

// here is a recursive definition of expressions 
// using priority and associativity groups.
syntax Expression 
  = id: Identifier id
  | null: "null"
  | left multi: Expression l "*" Expression r
  > left ( add: Expression l "+" Expression r
         | sub: Expression l "-" Expression r
         )
  | bracket "(" Expression ")"
  ;
\end{lstlisting}
\vspace*{-8pt}
\caption{Syntax example}
\label{fig:syntax_example}
\end{figure}

The main weakness of using Syntax Definition is that we lose control over how the parser handles invalid code. If the parser runs into invalid code it raises an error intended for the tool designer. This error is hard to read for the tool user and does not provide the necessary feedback for a game designer to fix their code. To address this issue, we create a \emph{'flexibile'} grammar. This means that our grammar should parse and accept invalid code, to an extent. We aim to design our grammar so that only severe structural errors can cause it to raise an error. To this end, we define keywords to make our definition more human-readable. However, we do not use them to exclude tokens to avoid a parser error. Our grammar is mostly made up of flexible lexicals grouped up under symbols. The main goal of our grammar is to assign "labels" to tokens so that our static checker knows what rules they should be checked against. Figure \ref{fig:PuzzleScript Lexicals} shows the lexicals, and Figure \ref{fig:example_symbol} shows a sample of our grammar that parses Objects. The latest version of the full grammar can be accessed on the project repository\footnote{\url{https://github.com/ClementJ18/ScriptButler/blob/main/src/PuzzleScript/Syntax.rsc}}.

The Syntax Definition also offers an opportunity to annotate the file with interesting metadata. This metadata can then be used by the syntax checker or IDE plugins to enrich and support the developer experience. For example, the \texttt{@Foldable} tag, allows the developer to collapse any piece of code that matches the symbol in the IDE to temporarily hide it. The design of our grammar does still present weaknesses. PuzzleScript's design presents several ambiguities which a formal grammar cannot handle. We mitigate the effect of these ambiguities by defining certain objects more rigidly. This approach directly counteracts our goal of keeping the grammar flexible so we carefully balance the two. 

The result of this section is a grammar that we can apply directly to a PuzzleScript file to generate a parse tree representing that game. The parse tree needs further manipulation to be usable but using a Syntax Definition fulfills our goal of creating a more readable version of PuzzleScript's grammar.

\begin{figure}[!t]
\begin{lstlisting}[language=rascal]
lexical LAYOUT 
	= [\t\r\ ]
	| ^ Comment Newlines
	> Comment
	;
layout LAYOUTLIST = LAYOUT* !>> [\t\r\ )];

lexical SectionDelimiter = [=]+ Newlines;
lexical Newlines = Newline+ !>> [\n];
lexical Comment = @Category="Comment" "(" (![()]|Comment)+ ")";
lexical Newline = [\n];
lexical ID = @Category="ID" [a-z0-9.A-Z#_+]+ !>> [a-z0-9.A-Z#_+] \ Keywords;
lexical Pixel = [TRUNCATED];
lexical LegendKey = Pixel+ !>> [TRUNCATED] \ Keywords;
lexical Spriteline = [0-9.]+ !>> [0-9.] \ Keywords;
lexical Levelline = Pixel+ !>> Pixel \ Keywords;
lexical String = ![\n]+ >> [\n];
lexical SoundIndex = [0-9]|'10' !>> [0-9]|'10';
lexical KeywordID = @Category="Key"[a-z0-9.A-Z_]+ !>> [a-z0-9.A-Z_] \ 'message';
lexical IDOrDirectional = @Category="ID" [\>\<^va-z0-9.A-Z#_+]+ !>> [\>\<^va-z0-9.A-Z#_+] \ Keywords;
\end{lstlisting}
\vspace*{-8pt}
\caption{PuzzleScript layout and lexicals}
\label{fig:PuzzleScript Lexicals}
\end{figure}

\begin{figure}[!t]
\begin{lstlisting}[language=rascal]    
syntax Objects = objects: SectionDelimiter? 'OBJECTS' Newlines SectionDelimiter? ObjectData+;
syntax Color = @category="Color" ID;
syntax Colors = Color+;
syntax ObjectName = @category="ObjectName" ID;

syntax ObjectData
	= @Foldable object_data: ObjectName LegendKey? Newline Colors Newline Sprite?
	| object_empty: Newlines
	;
	
syntax SpritePixel = @category="SpritePixel" SpriteP;

syntax Sprite 
    =  @Foldable sprite: 
       SpritePixel+ Newline
       SpritePixel+ Newline
       SpritePixel+ Newline 
       SpritePixel+ Newline
       SpritePixel+ Newline
    ;
\end{lstlisting}
\vspace*{-8pt}
\caption{Symbols for parsing Objects}
\label{fig:example_symbol}
\end{figure}
